# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Darshit Shah <darnir@gmail.com>
# Contributor: Gabriel B. Casella <gbc921@gmail.com>
# Modified By:  Chinmay Kamat <chinmaykamat@gmail.com>

_gitname=wget
pkgname=wget-git-progress-bar-patch
pkgver=v1.14.98.gcb358c0
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web with progress bar patch"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base')
depends=('openssl' 'libidn' 'util-linux')
optdepends=('ca-certificates: HTTPS downloads')
makedepends=('glibc' 'pcre' 'git' 'perl')
provides=('wget')
conflicts=('wget')
backup=('etc/wgetrc')
install=wget.install
source=("git://git.savannah.gnu.org/${_gitname}.git"
        "git://git.savannah.gnu.org/gnulib.git")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd $_gitname
  #Use the tag from the last commit.
  git describe --always | sed 's|-|.|g'
}

build() {
  patch -Np0 -i "$startdir"/wget_nv_pbar.diff || return 1
  
  cd $_gitname
  ./bootstrap --gnulib-srcdir=../gnulib
  ./configure --prefix=/usr --without-included-regex --enable-threads=posix --enable-dependency-tracking --with-ssl=openssl --enable-nls --sysconfdir=/etc
  make
  #make || make || return 1
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir"/ install

  cat >> "$pkgdir/etc/wgetrc" <<EOF
# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

# vim:set ts=2 sw=2 et:
