# Maintainer: Gabriel B. Casella <gbc921@gmail.com>
# Contributor: Chinmay Kamat <https://github.com/cdkamat>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Darshit Shah <darnir@gmail.com>

_gitname=wget
pkgname=wget-git-progress-bar-patch
pkgver=v1.14.99.gfb23bc0
pkgrel=1
pkgdesc="wget patched to make -no-verbose option outputs a progress bar"
arch=('i686' 'x86_64')
url="https://github.com/gbc921/aur/tree/master/submitted/wget-git-progress-bar-patch"
license=('GPL3')
groups=('base')
depends=('openssl' 'libidn' 'util-linux')
optdepends=('ca-certificates: HTTPS downloads')
makedepends=('glibc' 'pcre' 'git' 'perl')
provides=('wget')
conflicts=('wget')
backup=('etc/wgetrc')
install=wget.install
source=("git://git.savannah.gnu.org/${_gitname}.git"
        "git://git.savannah.gnu.org/gnulib.git")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd $_gitname
  #Use the tag from the last commit.
  git describe --always | sed 's|-|.|g'
}

build() {
  patch -Np0 -i "$srcdir"/../wget_nv_pbar.diff || return 1
  
  cd $_gitname
  ./bootstrap --gnulib-srcdir=../gnulib
  ./configure --prefix=/usr --without-included-regex --enable-threads=posix --enable-dependency-tracking --with-ssl=openssl --enable-nls --sysconfdir=/etc
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir"/ install

  cat >> "$pkgdir/etc/wgetrc" <<EOF
# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

# vim:set ts=2 sw=2 et:
