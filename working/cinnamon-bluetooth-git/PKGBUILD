# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Gabriel B Casella <gbc921@gmail.com>

_pkgname=cinnamon-bluetooth
pkgname=${_pkgname}-git
pkgver=13.18262be
pkgrel=1
epoch=1
pkgdesc="The Cinnamon Bluetooth Module"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cinnamon-bluetooth"
license=(GPL)
depends=('gtk3' 'cinnamon-control-center' 'gnome-bluetooth')
makedepends=('gnome-common' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=(!emptydirs)
install=cinnamon-bluetooth.install
source=("${_pkgname}"::git+https://github.com/linuxmint/cinnamon-bluetooth.git
        'cc-bluetooth-panel-archlinux.patch'
        'bluetooth-plugin-manager.c'
        'bluetooth-plugin-manager.h')
sha256sums=('SKIP'
            '50d1ac34956a9bc1c08f62c1d09e6e644ca9a52e9481ecf9bbfb06e7494af691'
            'd6be4c73728a66a6ee3b7e95a2e7fb5a59ce16b8c8ec99b117bf08d83909b7ba'
            'b62cc78664b382e3f1b1f51f8a80db16fe4d18cc57b6c26c4fdd621eec1ca473')

pkgver() {
  cd "${srcdir}"/"${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}"/"${_pkgname}"/panels/bluetooth/
  # patches Bluetooth Killswitch depreciation (http://goo.gl/JMTfD2)
  patch -Np0 -i "${srcdir}"/../cc-bluetooth-panel-archlinux.patch || return 1
  # and patches https://github.com/linuxmint/muffin/issues/141
  # even though is depreciated <https://mail.gnome.org/archives/commits-list/2013-December/msg01065.html>
  cp "${srcdir}"/../bluetooth-plugin-manager.* "${srcdir}"/"${_pkgname}"/panels/bluetooth/
}

build() {
  cd "${srcdir}"/"${_pkgname}"
  
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var 

  make
}

package() {
  cd "${srcdir}"/"${_pkgname}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
