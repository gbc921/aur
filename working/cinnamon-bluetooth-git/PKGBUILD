# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Gabriel B Casella <gbc921@gmail.com>

_pkgname=cinnamon-bluetooth
pkgname=${_pkgname}-git
pkgver=13.18262be
pkgrel=1
epoch=1
pkgdesc="The Cinnamon Bluetooth Module"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cinnamon-bluetooth"
license=(GPL)
depends=('gtk3' 'cinnamon-control-center' 'gnome-bluetooth')
makedepends=('gnome-common' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=(!emptydirs)
install=cinnamon-bluetooth.install
source=("${_pkgname}"::git+https://github.com/linuxmint/cinnamon-bluetooth.git
        'cc-bluetooth-panel-killswitch.patch'
        'cc-bluetooth-panel-plugins.patch')
sha256sums=('SKIP'
            'ff7b036dedb3fae38fc64af6782ce3718be530299aa4118cf7c478c2023c3b20'
            'dd9b0cdb2dc0cec3fd8ad7f1475e29c67a94bac9d20fc3fededf1e774bef02e0')

pkgver() {
  cd "${srcdir}"/"${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}"/"${_pkgname}"/panels/bluetooth/

  # patches Bluetooth Killswitch depreciation (http://goo.gl/JMTfD2)
  patch -Np0 -i "${srcdir}"/cc-bluetooth-panel-killswitch.patch || return 1

  # and patches plugin manager depreciation <https://github.com/linuxmint/muffin/issues/141>
  # <https://mail.gnome.org/archives/commits-list/2013-December/msg01065.html>
  patch -Np0 -i "${srcdir}"/cc-bluetooth-panel-plugins.patch || return 1
}

build() {
  cd "${srcdir}"/"${_pkgname}"
  
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var 

  make
}

package() {
  cd "${srcdir}"/"${_pkgname}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
