# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: gbc921 <gbc921@gmail.com>
# Based on cinnamon-session-git PKGBUILD

_pkgname=cinnamon-session
pkgname=${_pkgname}-git
pkgver=112.912a071
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=('cinnamon-desktop' 'upower' 'gconf' 'libcanberra' 'libsm')
makedepends=('intltool' 'mesa' 'json-glib' 'xtrans' 'gnome-common' 'git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
options=('!emptydirs')
install=${_pkgname}.install
url="http://cinnamon.linuxmint.com/"
source=(${_pkgname}::git+https://github.com/linuxmint/cinnamon-session.git)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $_pkgname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/cinnamon-session \
      --sbindir=/usr/bin --disable-schemas-compile --enable-systemd
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
